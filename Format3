(vl-load-com)

;; UnFormat String function by Lee Mac
(defun LM:UnFormat (str mtx / _replace rx)
  (defun _replace (new old str)
    (vlax-put-property rx 'pattern old)
    (vlax-invoke rx 'replace str new)
  )
  (if (setq rx (vlax-get-or-create-object "VBScript.RegExp"))
    (progn
      (setq str
        (vl-catch-all-apply
          (function
            (lambda ()
              (vlax-put-property rx 'global     actrue)
              (vlax-put-property rx 'multiline  actrue)
              (vlax-put-property rx 'ignorecase acfalse)
              (foreach pair
                '(
                  ("\032"    . "\\\\\\\\")
                  (" "       . "\\\\P|\\n|\\t")
                  ("$1"      . "\\\\(\\\\[ACcFfHLlOopQTW])|\\\\[ACcFfHLlOopQTW][^\\\\;]*;|\\\\[ACcFfHLlOopQTW]")
                  ("$1$2/$3" . "([^\\\\])\\\\S([^;]*)[/#\\^]([^;]*);")
                  ("$1$2"    . "\\\\(\\\\S)|[\\\\](})|}")
                  ("$1"      . "[\\\\]({)|{")
                )
                (setq str (_replace (car pair) (cdr pair) str))
              )
              (if mtx
                (_replace "\\\\" "\032" (_replace "\\$1$2$3" "(\\\\[ACcFfHLlOoPpQSTW])|({)|(})" str))
                (_replace "\\"   "\032" str)
              )
            )
          )
        )
      )
      (vlax-release-object rx)
      (if (null (vl-catch-all-error-p str))
        str
      )
    )
  )
)

(defun c:adjustRowsAndFormat ( / itemNumber maxItemNumber maxHeight tables table rowCount colCount i row currentHeight cellContent unformattedContent textHeight verticalMargin horizontalMargin minRowHeight)
  ;; Prompt the user to enter the item number
  (setq itemNumber (getint "\nEnter the lowest item number in this F&ID: "))
  (setq maxItemNumber (getint "\nEnter the highest item number in this F&ID: "))
  ;; Initialize the maximum height
  (setq maxHeight 0)

  ;; Modify the following variables to change text height and cell margins
  (setq textHeight 0.1389) ;; Modify this number to change the table's text height
  (setq verticalMargin 0.06) ;; Modify this number to change the vertical margin of the table cells
  (setq horizontalMargin 0.06) ;; Modify this number to change the horizontal margin of the table cells
  
  (setq minRowHeight 0) ;; Initialize variable for minimum row height

  ;; Get all entities in the drawing
  (setq tables (vla-get-ModelSpace (vla-get-ActiveDocument (vlax-get-acad-object))))
  (print "\nRetrieved all entities in the drawing.")
  
  ;; Format all tables
  (vlax-for ent tables
    (if (eq (vlax-get-property ent 'ObjectName) "AcDbTable")
      (progn
        (setq table ent)
        (setq rowCount (vla-get-Rows table)) ;; vla-get-rows returns the number of rows and sets it to rowCount
        (setq colCount (vla-get-Columns table))
        (print (strcat "Processing table with " (itoa rowCount) " rows and " (itoa colCount) " columns."))

        ;; Iterate over all rows to set the format
        (setq i 0)
        (while (< i rowCount)
          ;; Set the column widths
          (vla-setcolumnwidth table 0 0.6415) ;; Set the first column to 0.6415 units wide
          (vla-setcolumnwidth table 1 2.1142) ;; Set the second column to 2.1142 units wide
          (vla-setcolumnwidth table 2 2.6856) ;; Set the third column to 2.6856 units wide
          (vla-setcolumnwidth table 3 2.5281) ;; Set the fourth column to 2.5281 units wide
          (vla-setcolumnwidth table 4 0.9262) ;; Set the fifth column to 0.9262 units wide
          (vla-setcolumnwidth table 5 6.5582) ;; Set the sixth column to 6.5582 units wide
          
          ;; Set the text height and cell margins
          (vla-settextheight table acDataRow textHeight) ;; Set all data rows to the text height
          (vla-put-VertCellMargin table verticalMargin) ;; Set the vertical margin for the table cells
          (vla-put-HorzCellMargin table horizontalMargin) ;; Set the horizontal margin for the table cells
          
          (setq minRowHeight (vla-getminimumrowheight table i)) ;; Get the minimum row height based on the text
          (vla-setRowHeight table i minRowHeight) ;; Set the minimum row height as the row height
          (vla-setcellalignment table i 0 acMiddleCenter) ;; Set first column to middle center alignment
          (vla-setcellalignment table i 1 acMiddleLeft) ;; Set second column to left center alignment
          (vla-setcellalignment table i 2 acMiddleLeft) ;; Set third column to left center alignment
          (vla-setcellalignment table i 3 acMiddleLeft) ;; Set fourth column to left center alignment
          (vla-setcellalignment table i 4 acMiddleCenter) ;; Set fifth column to middle center alignment
          (vla-setcellalignment table i 5 acMiddleLeft) ;; Set sixth column to left center alignment
    
          (setq i (+ i 1)) ;; Add 1 to counter variable
        ) ;; Close while loop
      )
    )
  )
  
  ;; Adjust row heights for each item number
  (while (<= itemNumber maxItemNumber)
    ;; Iterate over all entities and filter the tables
    (vlax-for ent tables
      (if (eq (vlax-get-property ent 'ObjectName) "AcDbTable")
        (progn
          (setq table ent)
          (setq rowCount (vla-get-Rows table))
          (setq colCount (vla-get-Columns table))
          (print (strcat "Processing table with " (itoa rowCount) " rows and " (itoa colCount) " columns."))

          ;; Iterate over all rows to find the maximum height of the row with the specified item number
          (setq i 0)
          (while (< i rowCount)
            ;; Use vla-getText to get the cell content
            (setq cellContent (vla-getText table i 0))
            ;; Unformat the cell content for comparison
            (setq unformattedContent (strcase (LM:UnFormat cellContent nil)))
            (print (strcat "Checking row " (itoa i) " for item number. Unformatted cell content: " unformattedContent))
            (if (= unformattedContent (itoa itemNumber))
              (progn
                (setq currentHeight (vla-getRowHeight table i))
                (if (> currentHeight maxHeight)
                  (setq maxHeight currentHeight)
                )
              )
            )
            (setq i (1+ i))
          )
        )
      )
    )

    (print (strcat "Maximum height found: " (rtos maxHeight 2 4)))

    ;; Adjust the row heights
    (vlax-for ent tables
      (if (eq (vlax-get-property ent 'ObjectName) "AcDbTable")
        (progn
          (setq table ent)
          (setq rowCount (vla-get-Rows table))
          (setq colCount (vla-get-Columns table))

          ;; Iterate over all rows to adjust the height
          (setq i 0)
          (while (< i rowCount)
            ;; Use vla-getText to get the cell content
            (setq cellContent (vla-getText table i 0))
            ;; Unformat the cell content for comparison
            (setq unformattedContent (strcase (LM:UnFormat cellContent nil)))
            (if (= unformattedContent (itoa itemNumber))
              (progn
                (vla-setRowHeight table i maxHeight)
                (print (strcat "Adjusting height of row " (itoa i) " in table."))
                
              )
            )
            (setq i (1+ i))
          )
        )
      )
    )
    (setq maxHeight 0)
    (setq itemNumber(+ itemNumber 1)) 
  ) 
  (princ "\nRow heights adjusted and tables formatted.")
  (princ)
)

(princ "\nType adjustRowsAndFormat to adjust row heights and format tables.\n")
(princ)
