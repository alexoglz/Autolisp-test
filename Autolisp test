;;---Organization: GE Vernova-----------------------------------------------------;;
;;---Title: formatBom-------------------------------------------------------------;;
;;---Lean Challenge 2024----------------------------------------------------------;;
;;---Project Name: Simplifying F&ID BOM update process----------------------------;;
;;---Project Leaders: Eliseo Regla, Yunuen Pe√±a-----------------------------------;;
;;---Lean Mentor: Rafael Lozano---------------------------------------------------;;
;;---Authors: Alan Acosta, Alejandro Gonzalez, Rodrigo Valdivia, Ifunanya Amene---;;
;;---Department: Aeroderivative Fluid Systems-------------------------------------;;
;;---Description: This AutoLISP code prints a list of the layers that are---------;;
;;---turned on.------------------------------------------------------------;;
;;---Instructions: type "PrintLayerNames" in AutoCAD and run the command.  Enter--;;
;;---the number of layers to list (e.g., 6) and press Enter. Click the desired----;;
;;---location in the drawing. The script will inform you how many layers----------;;
;;---were listed and how many are still available.--------------------------------;;
;;---Refer to first part of main function section (defun c:PrintLayerNames ()).---;;

(defun custom-subseq (1st start end)
  (setq result '())
  (setq i start)
  (while (< i end)
    (setq result (append result (list (nth i 1st))))
    (setq i (1+ i))
    )
  result
  )

(defun sort-layers (layer-list)
  (setq layer-names (mapcar 'vla-get-Name layer-list))
  (setq sorted-layer-names (vl-sort layer-names '<))
  (setq sorted-layers '())
  (foreach layer-name sorted-layer-names
    (setq layer (vl-some '(lambda (x) (if (eq layer-name (vla-get-Name x)) x)) layer-list))
    (if layer (setq sorted-layers (append sorted-layers (list layer)))))
  sorted-layers
  )

(defun C:PrintLayerNames ()
  (setq doc(vla-get-ActiveDocument (vlax-get-Acad-Object)))
  (setq layers (vla-get-Layers doc))
  (setq layerList '())

  ;Get all visible and active layers
  (vlax-for layer layers
    (if (and
	  (= (vla-get-LayerOn layer) :vlax-true)
	  (/= (vla-get-Freeze layer) :vlax-true)
	  (/= (vla-get-Lock layer) :vlax-true)
	  )
      (setq layerList (cons layer layerList))
      )
    )

  ;Order layers numerically
  (setq layerList (sort-layers layerList))

  ;Show number of visible layers to the user
  (setq numLayers (length layerList))
  (prompt (strcat "\nThere are " (itoa numLayers) " visible layers."))

  (setq remainingLayers numLayers)

  (while (> remainingLayers 0)
    (setq numElements (getint (strcat "\nEnter number of elements for the next list (remaining " (itoa remainingLayers) "): ")))

    (if (or (null numElements) (<= numElements 0) (> numElements remainingLayers))
      (prompt "\nInvalid number of elements. Please enter a valid number.")
      (progn
	(setq pt (getpoint "\nSelect point to insert layer list: "))

	(if pt
	  (progn
	    (setq yOffset 0.0)
	    (setq textHeight 0.1250); Text Height

	    ;Insert text of visible layers
	    (foreach layer (custom-subseq layerList 0 numElements)
	      (setq layerName (vla-get-Name layer))
	      (vla-put-ActiveLayer doc layer); Change active layer to corresponding layer
	      (entmakex (list
		    (cons 0 "TEXT")
		    (cons 10 (list (car pt) (+ (cadr pt) yOffset) 0.0))
		    (cons 40 textHeight) ;Text height
		    (cons 1 layerName)
		    (cons 7 "Standard") ; Text style
		    (cons 72 0) ; Aligned to the left
		    (cons 11 (list (car pt) (+ (cadr pt) yOffset) 0.0))
		    (cons 71 0) ; Text generated
		    (cons 50 0.0) ; Text rotation
		    (cons 73 1) ;Vertical alignment
		    ))
	(setq yOffset (- yOffset (* textHeight 1.5))) ; Decrease the position for the next line
	      )

	    ; Eliminate layers already used from the list
	    (setq layerList (custom-subseq layerList numElements (length layerList)))
	    (setq remainingLayers (- remainingLayers numElements))
	    )
	  )
	)
      )
    )

  (princ)
  )
(princ "\nType 'PrintLayerNames' to insert text for visible layers in multiple lists.\n")
